//===============================================================\\
//                _____                                          ||
//               /  __ \                                         ||
//               | /  \/_ __ ___  _ __  _   _ ___                ||
//               | |   | '__/ _ \| '_ \| | | / __|               ||
//               | \__/\ | | (_) | | | | |_| \__ \               ||
//                \____/_|  \___/|_| |_|\__,_|___/               ||
//                     Scripts                                   ||
//===============================================================||
// = Versão:                                                     ||
// - 6.8                                                         ||
//===============================================================||
// = Alterações:                                                 ||
// - Tradução [?]                                                ||
// - Revisão e Correções [Spell Master]                          ||
// - Adicionado função "Sex" [Spell Master]                      ||
//===============================================================||
// = Descrição:                                                  ||
// - Funções usadas pelas funcionárias Kafra e Eventos Incríveis ||
//===============================================================//

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
// Traduzido por: Lucas Campos Achcar
//==========================================================================

function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	// Display Kafra Welcome Message
	switch (getarg(0)) {
		default:
		case 0:
			// Default message (obsolete)
			mes "[Funcionária Kafra]";
			mes "Bem vindo a Corporação Kafra.";
			mes "Os serviços da Kafra estão sempre ao seu lado.";
			mes "Como eu posso ajudar você?";
			break;
		case 1:
			// Niflheim specific message
			mes "[Funcionária Kafra]";
			mes "^666666W-bemmm-vind"+(Sex?"o":"a")+"a Coor-portaçao Kaaa-fraa...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			@GID = getcharid(2);
			mes "[Funcionária Kafra]";
			mes "Bem vind"+(Sex?"o":"a")+".";
			mes "Membro da GUIDA ^ff0000" +getguildname(@GID)+"^000000.";
			mes "A Corporação Kafra vai estar com você onde você for.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[Funcionária Kafra]";
			mes "Então, você veio de um lugar tão longe estudar nossa cultura, ou apenas para turismo?";
			mes "Em ambos os casos, Porque não fica mais um pouco?";
			mes "O ar é eterno passado com o agradável aroma das flores silvestres.";
			break;
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[Funcionária Kafra]";
			mes "Com outros muitos serviços da locação Kafras, você nunca mais vai andar tanto.";
			break;
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (true) {
		deletearray @K_Menu0$;
		if (getarg(0) == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"Usar Armazém","Usar Armazém da Guild","Alugar Carrinho","Usar Serviço de Teletransporte","Cancelar";
		}
		else {
			switch (getarg(1)) {
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"Salvar","Usar Armazém","Cancelar"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"Usar Armazém","Cancelar"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Checar Outros Serviços","Cancelar"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Checar Outros Serviços","Cancelar"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"Usar Armazém","Checar Outros Serviços","Cancelar"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"Salvar","Usar Armazém","Alugar Carrinho","Cancelar"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Checar Outros Serviços","Cancelar"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"Usar Armazém","Alugar Carrinho","Usar Serviço de Teletransporte","Checar Outros Serviços","Cancelar"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"Usar Armazém","Salvar","Alugar Carrinho","Cancelar"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"Salvar","Usar Armazém","Usar Serviço de Teletransporte","Alugar Carrinho","Checar Outros Serviços","Cancelar"; break;
			}
		}
		.@menu$ = "";
		for (.@i = 0; .@i < getarraysize(@K_Menu0$); ++.@i)
			.@menu$ += @K_Menu0$[.@i] + ":";
		.@j = select(.@menu$) - 1;
		if (@K_Menu0$[.@j] == "Salvar") {
			return;
		}
		else if (@K_Menu0$[.@j] == "Usar Armazém") {
			// Do not charge for Guild Storage
			if(getarg(0) == 2)
				callfunc "F_KafStor",2,0,0,"[Funcionária Kafra]";
			else
				callfunc "F_KafStor",0,getarg(3),getarg(0),"[Funcionária Kafra]";
			next;
		}
		else if (@K_Menu0$[.@j] == "Usar Serviço de Teletransporte") {
			// Display Einbroch "Sem serviço de teletransporte" notice.
			if (getarg(1) != 4) {
				callfunc "F_KafTele",getarg(0),"[Funcionária Kafra]";
			}
			else {
				mes "[Funcionária Kafra]";
				mes "Por causa do ^FF0000Acordo Transporte Limitado^000000.";
				mes "A Corporação Kafra não pode providênciar o serviço de teletransporte para a república Schwaltzvalt.";
				next;
				mes "[Funcionária Kafra]";
				mes "Nos pedimos para o senhor, por favor para usar o serviço de aviação.";
				mes "Obrigado pela sua paciência, entendimento e cooperação.";
			}
			next;
		}
		else if (@K_Menu0$[.@j] == "Alugar Carrinho") {
			if(callfunc("F_KafCart",getarg(0),getarg(4),"[Funcionária Kafra]") == 1)
				next;
		}
		else if (@K_Menu0$[.@j] == "Checar Outros Serviços") {
			callfunc "F_KafInfo",getarg(2);
			next;
		}
		else if (@K_Menu0$[.@j] == "Cancelar"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		}
		else if (@K_Menu0$[.@j] == "Usar Armazém da Guild") {
			callfunc "F_KafStor",1,0,0,"[Funcionária Kafra]";
			next;
		}
	}
}

//===================================================
// Storage Function
//=   arg(0): Specifies the type of storage that has been requested:
//            0 = Regular storage
//            1 = Guild storage
//            2 = Regular storage, in a castle (free of charge)
//=   arg(1): Cost to use storage.
//=   arg(2): The type of NPC this is being called from. Notable values:
//            0,2-5 = Regular Kafra
//            1 = Niflheim Kafra (special messages)
//            6 = Zonda/Cool Event Staff (no RESRVPTS)
//=   arg(3): NPC title to display.
//===================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if (getarg(0) == 1) {
		if (guildopenstorage()) {
			mes getarg(3);
			mes "Me desculpe, mas outro membro da guild está usando o armazém nesse exato momento.";
			mes "Por favor, aguarde até que seja finalizado.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (basicskillcheck() && getskilllv(NV_BASIC) < 6) {
		mes getarg(3);
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666Des-cul-paaaaa, V-ocê é umm-mmm novato...";
			mes "Pree-cisa de habilidades B-asica L-level 6...^000000";
			return;
		}
		mes "Me desculpe, mas você precisa de habilidades básicas Nivel 6 para usar o serviço de Armazém.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (getarg(0) != 2) {
		// Consume "Free Ticket for Kafra Storage" if available.
		if (countitem(Cargo_Free_Ticket)) {
			delitem Cargo_Free_Ticket, 1;
		}
		else {
			if (Zeny < getarg(1)) {
				mes getarg(3);
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "M-more z-zeny...!";
					mes "N-neeed 150... zeny...";
					mes "Ergh! T-taking bl-blood~!^000000";
					return;
				}
				// Standard Message
				mes "Me desculpe, mas você não tem zeny suficiente para usar o serviço de Armazém.";
				mes "A taxa é de "+getarg(1)+" zeny.";
				return;
			}
			Zeny -= getarg(1);
			if (getarg(2) != 6)
				RESRVPTS += getarg(1) / 5;
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		if (getarg(2) == 1) {
			// Niflheim specific Message
			percentheal 0,-10;
			mes getarg(3);
			for (.@i = 0; .@i < 5; ++.@i)
				mes "^666666Thank you.. for... using...^000000";
		}
		else if (getarg(2) == 6) {
			// Zonda
			mes getarg(3);
			mes "Deixe-me abrir o seu armazém pessoal imediatamente.";
			mes "Obrigado pelo seu apoio por usar nossos serviços.";
			mes "Tenha um bom dia~";
		} 
		else if(#kafra_code){
			mes "Aqui, digite a senha para abrir o armazém.";
			mes "Obrigado por usar os serviços Kafra.";
		}
		else {
			// Normal message
			mes getarg(3);
			mes "Aqui, deixe-me abrir o armazém para o senhor.";
			mes "Obrigado por usar os serviços Kafra.";
		}
	}
	callfunc("F_CheckKafCode"); //check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//=   arg(2): The type of NPC this is being called from. Notable values:
//            2 = Guild Castle Kafra (don't consume tickets)
//            6 = Zonda/Cool Event Staff (no RESRVPTS)
//=   arg(1): NPC title to display.
//===================================================
function	script	F_KafTele	{
	mes getarg(1);
	mes "Por favor, escolha seu destino.";
	next;
	.@menu$ = "";
	for (.@i = 0; .@i <= getarraysize(@wrpD$); ++.@i)
		.@menu$ += @wrpC$[.@i] + ":";
	.@j = select(.@menu$) - 1;
	if (@wrpC$[.@j] == "Cancelar")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(Warp_Free_Ticket) > 0 && getarg(0) != 2) {
		delitem Warp_Free_Ticket, 1;
	}
	else {
		if (Zeny < @wrpP[.@j]) {
			mes getarg(1);
			mes "Me desculpe, mas você não tem zeny suficiente para o teletransporte.";
			mes "A taxa para "+@wrpD$[.@j]+" é de "+@wrpP[.@j]+" zeny.";
			close2;
			cutin "", 255;
			end;
		}
		Zeny -= @wrpP[.@j];
		if (getarg(0) != 6)
			RESRVPTS += @wrpP[.@j] / 16;
	}
	cutin "", 255;
	if (@wrpD$[.@j]      == "Al De Baran")     warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta")         warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo")          warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Farol de Pharos") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen")          warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") {
		if (RENEWAL) { warp "izlude", 128, 98; } else { warp "izlude", 91, 105; }
	}
	else if (@wrpD$[.@j] == "Juno")                warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir")             warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc")              warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Calabouços dos Orcs") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon")               warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera")            warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala")              warp "umbala", 100, 154;
	else if (@wrpD$[.@j] == "Veins")               warp "veins",205,101;
	else if (@wrpD$[.@j] == "Rachel")              warp "rachel",115,125;
	end;
}


//===================================================
// Cart Function
//=   arg(0): The type of NPC this is being called from. Notable values:
//            0,1,3-5 = Regular Kafra
//            2 = Guild Castle Kafra (don't consume tickets)
//            6 = Zonda/Cool Event Staff (no RESRVPTS)
//=   arg(1): Cost to rent a cart.
//=   arg(2): NPC title to display.
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if (BaseClass != Job_Merchant) {
		mes getarg(2);
		mes "Me desculpe, mas o serviço de alugar carrinho é apenas disponivel para Mercadores e Evoluções.";
		return 1;
	}
	if (getskilllv(MC_PUSHCART) == 0) {
		mes getarg(2);
		mes "Você só pode alugar um carrinho depois que aprender a habilidade Carrinho de Mão.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	if (checkcart() == 1) {
		mes getarg(2);
		mes "Você já tem um carrinho equipado.";
		mes "Infelizmente não podemos emprestar mais que um para cada cliente.";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if (countitem(Cart_Free_Ticket) > 0 && getarg(0) != 2) {
		delitem Cart_Free_Ticket, 1;
	}
	else {
		mes getarg(2);
		mes "O aluguel do carrinho é de "+getarg(1)+" zeny.";
		mes "Você gostaria de alugar?";
		next;
		if (select("Alugar Carrinho.:Cancelar") == 2)
			return 0;
		if (Zeny < getarg(1)) {
			mes getarg(2);
			mes "Me desculpe, mas você não tem zeny suficiente para pagar o Aluguel do Carrinho.";
			mes "A taxa é de "+getarg(1)+" zeny.";
			return 1;
		}
		Zeny -= getarg(1);
		if (getarg(0) != 6)
			RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//.@block = 1;
	setarray .@m$, "Verificar Reserva Especial de Pontos","Serviço de Senha do Armazém","Localizações dos Empregados da Kafra","Cancelar";
	if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight: remove Storage Password and Kafra Employee Locations
	else if (.@block) deletearray .@m$[1],1; // Remove Storage Password
	while (true) {
		.@j = select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "Verificar Reserva Especial de Pontos") {
			mes "[Funcionária Kafra]";
			mes "Deixe-me ver...";
			mes strcharinfo(0) + "...";
			mes "Ah, Você tem no total ";
			mes RESRVPTS+ " Reserva especial de pontos.";
			next;
			mes "[Funcionária Kafra]";
			mes "Você pode trocar os seus Pontos de Reserva Especial por recompensas no Escritório Principal Kafra em toda Al De Baran.";
			mes "Por favor, utilize nosso conveniênte serviço para ver o benefícios de nossos programas de recompensas.";
			next;
			if(getarg(0) == 1)
				return;
		}
		else if (.@m$[.@j] == "Localizações dos Empregados da Kafra") {
			for (.@i = 0; .@i <= 3; ++.@i)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (.@i = 0; .@i <= 3; ++.@i)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		}
		else if (.@m$[.@j] == "Serviço de Senha do Armazém") {
			callfunc("F_SetKafCode","[Funcionária Kafra]","Serviços Kafra");
		}
		else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Funcionária Kafra]";
	if (getarg(1) == 1) {
		// Save
		mes "Seu ponto de renascimento foi salvo aqui "+getarg(2)+".";
		mes "Obrigado por usar os serviços Kafra.";
	}
	else if (getarg(0) == 0 || getarg(0) == 5) {
		// Generic End
		mes "Nós, aqui da Corporação Kafra,";
		mes "estamos sempre nos esforçando para providênciar a você o melhor. Nós esperamos atender as suas necessidades e padrões de excelência de aventura.";
	}
	else if (getarg(0) == 1) {
		// Niflheim End
		percentheal 0,-25;
		mes "^666666Kaffffra nun-ca morrrr-e...";
		mes "On se--mpree aoooo se-uuu laaaddoo~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "Entre com sua senha:";
	@code_ = 0;
	input @code_;
	if (@code_ != #kafra_code) {
		dispbottom "Senha do armazém errada.";
		close2;
		cutin "",255;
		end;
	}
	@kafcode_try = 0;
	@code_ = 0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	mes getarg(0);
	if (#kafra_code) {
		mes "Seu armazém está protegido por senha.";
		mes "O que você gostaria de fazer agora?";
		next;
		switch (select("Mudar senha antiga -> 5000z:Remover senha do armazém -> 1000z:Cancelar")) {
			case 1:
				mes getarg(0);
				mes "Mas primeiro, por favor, digita sua ^0000FFantiga senha^000000.";
				@code = callfunc("F_EntKafCode");
				if (!@code || @code != #kafra_code) {
					mes "Senha errada.";
					mes "Você não entrou com uma nova senha.";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,getarg(0),getarg(1);
				break;
			case 2:
				mes getarg(0);
				mes "Por favor, Digite sua senha para ser removida.";
				@code = callfunc("F_EntKafCode");
				if (!@code) {
					mes "A senha foi removida.";
					emotion e_hmm;
					break;
				}
				next;
				mes getarg(0);
				if (Zeny < 1000) {
					mes "Você não tem zeny suficiente.";
					emotion e_cash;
					break;
				}
				Zeny -= 1000;
				if (@code == #kafra_code) {
					#kafra_code = 0;
					mes "Você removeu com sucesso a senha do Armazém.";
					mes "Obrigado por usar "+getarg(1)+".";
					emotion e_thx;
				} else {
					mes "Senha errada.";
					mes "Nos vamos retornar seus 1000z.";
					mes "Obrigado, na próxima vez entre com sua senha correta.";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes getarg(1)+" orgulhosamente apresenta um novo serviço:";
		mes "Armazém protegido por senha.";
		next;
		switch (select("Digite uma nova senha -> 5000z:Cancelar")) {
			case 1: callsub S_SET,getarg(0),getarg(1); break;
			case 2: break;
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes getarg(0);
	mes "Agora entre com uma ^FF0000nova senha^000000 para proteger seu armazém de possiveis ladrões";
	@code = callfunc("F_EntKafCode");
	if (!@code) {
		mes "A senha não foi alterada.";
		emotion e_hmm;
		return;
	}
	next;
	mes getarg(0);
	if (Zeny < 5000) {
		mes "Você não tem zeny suficiente.";
		emotion e_cash;
		return;
	}
	Zeny -= 5000;
	#kafra_code = @code;
	mes "Você protegeu seu armazém com uma senha secreta.";
	mes "Obrigado por usar "+getarg(1)+".";
	emotion e_thx;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Entre com o número 1000~10000000:";
	@code_ = 0;
	++@kafcode_try;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Hack: Tentou descobrir a senha do armazém.";
	}
	if (input(@code_) == 1) {
		mes "Você não pode usar senhas grandes.";
		return 0;
	}
	if (@code_ < 1000) {
		mes "Você não deveria usar senhas curtas.";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$;
	deletearray @wrpD$;
	deletearray @wrpP$;
	deletearray @viewpX;
	deletearray @viewpY;
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$, "Izlude", "Geffen", "Payon", "Morroc", "Calabouço dos Orcs", "Alberta";
		setarray @wrpP, 600, 1200, 1200, 1200, 1200, 1800;
		setarray @viewpX, 151, 29, 282, 152;
		setarray @viewpY, 29, 207, 200, 326;
	}
	else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP, 1200, 1800, 1800;
		setarray @wrpD$, "Payon", "Morroc", "Prontera";
		setarray @viewpX, 28, 113, 0, 0;
		setarray @viewpY, 229, 60, 0, 0;
	}
	else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP, 1200, 1200, 1800, 1700;
		setarray @wrpD$, "Geffen", "Juno" , "Izlude", "Mjolnir";
	}
	else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$, "Morroc", "Comodo", "Umbala";
		setarray @wrpP, 1800, 1200, 1800;
	}
	else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$, "Comodo", "Morroc";
		setarray @wrpP, 1200, 1200;
	}
	else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$, "Prontera", "Al De Baran", "Calabouço dos Orcs", "Mjolnir";
		setarray @wrpP, 1200, 1200, 1200, 1700;
		setarray @viewpX, 120, 203;
		setarray @viewpY, 62, 123;
	}
	else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$, "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP, 1200, 1200, 1200, 1800;
	}
	else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$, "Prontera", "Payon", "Alberta", "Comodo", "Comodo";
		setarray @wrpP, 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX, 156, 163, 28, 292;
		setarray @viewpY, 97, 260, 167, 211;
	}
	else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$, "Comodo";
		setarray @wrpP, 1800;
	}
	else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$, "Prontera", "Alberta", "Morroc";
		setarray @wrpP, 1200, 1200, 1200;
	}
	else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$, "Al De Baran";
		setarray @wrpP, 1200;
		setarray @viewpX, 328, 278, 153, 0;
		setarray @viewpY, 108, 221, 187, 0;
	}
	else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$, "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP, 600, 1200, 1200, 1200, 1800;
	}
	else if (strnpcinfo(4) == "rachel") {
		setarray @wrpD$, "Veins";
		setarray @wrpP, 2200;
	}
	else if (strnpcinfo(4) == "veins") {
		setarray @wrpD$, "Rachel";
		setarray @wrpP, 2200;
	}
	for (.@i = 0; .@i < getarraysize(@wrpD$); ++.@i)
		@wrpC$[.@i] = @wrpD$[.@i]+" -> "+@wrpP[.@i];
	@wrpC$[getarraysize(@wrpC$)] = "Cancelar";
	return;
}


